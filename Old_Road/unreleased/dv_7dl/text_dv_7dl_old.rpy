label alt_day3_dv_reunion_old:
    scene bg ext_houses_night_7dl with dissolve
    play ambience ambience_camp_center_evening fadein 3
    "Я окончательно развернулся и ушёл, оставляя Алису наедине с её дурацкими мыслями, идиотской ревностью и ничем не обоснованными претензиями."
    "А в голове продолжали крутиться гитарные переборы…"
    play music music_7dl["deadman"] fadein 3
    "Ночной лагерь всё время разный."
    "В первый день он был тёплым, немного пугающим и в чём-то даже томным."
    "Во второй я будто проснулся, и всё было такое яркое-яркое…"
    "Сегодня же ко всему приплетается чуть терпкий хинный вкус полыни-разочарования."
    "Из-за деревьев слышна музыка, под неё танцевали пионеры, что-то крича."
    "На аллеях уже видны парочки, у колонки водопровода уже стояла очередь из нескольких мальчиков, наплясавшихся так, что рубашки пропотели насквозь."
    "В порядке живой очереди они ставили голову под поток ледяной воды, в восторге ухали и отскакивали."
    "Ноги почему-то вывели меня к складу."
    scene bg ext_warehouse_night_7dl with dissolve
    "Губы сами сложились в улыбку, когда я вспомнил, как мы с Электроником прятались здесь от гнева Двачевской."
    "Я прогонял в памяти всё, что нас связывало, всё, что мы пережили и могли бы вспомнить вместе."
    "А ночь между тем вступила в полные права, над головой распахнулось бездонное чёрное небо."
    "Над головой стояли звёзды, сегодня какие-то особенно блеклые и невыразительные…"
    "…а я вдруг понял, что меня, в сущности, готовы отпустить."
    dreamgirl "Накричал на девочку и свалишь, не попытавшись извиниться?"
    dreamgirl "Достойный поступок, ничего не скажешь."
    "Видимо, я не оправдал надежд даже своей шизофрении. Видимо, я и правда безнадёжен."
    "Мне стоит лишь внятно выразить просьбу — и я проснусь у себя дома, носом в клавиатуру."
    dreamgirl "Остановись, придурок!"
    "По левую руку всходила луна, и я, обернувшись в её сторону, набрал полную грудь воздуха."
    "И заговорил:"
    scene bg ext_warehouse_blurry_7dl
    "Реальность вздрогнула, впитывая мои слова."
    me "Пожалуйста…"
    dreamgirl "Стой!"
    me "Пожалуйста. Я очень хочу домой. Дайте мне уй…"
    scene black
    $ prolog_time()
    with fade2
    if loki:
        show blink
        scene bg ext_winterpark_7dl
        with dissolve
        pause(1.5)
        "За свою глупость я заплатил сполна."
        "Как жаль, что некому предъявить оплаченный счёт."
        "Холод забрался под кожу, и сил хватило только на то, чтобы тихо улыбнуться."
        "Я дурак. И я очень виноват."
        "Надеюсь, в следующей жизни у меня будет шанс всё исправить."
        play sound sfx_7dl["aunl"]
        show acm_logo_va_deep with moveinright:
            pos (1600, 1020)
        pause(4.4)
        with vpunch
    elif herc:
        play music "<to 52.94>" + music_7dl["herc_death"] fadein 5
        scene black with fade
        "Просто однажды дверь в лето открылась и выпустила наружу того, кто оказался безнадежен даже по меркам страны снов."
        "И чем бы ни оказалось слепое пятно в моей голове — уверен, это всё равно лучше, чем несбывшаяся мечта."
        "Несбывшаяся рыжая мечта с огромными янтарными глазами."
        "Пора домой."
        scene bg int_store_7dl with fade
        "Ты принимаешь правила игры. Ты забываешь о том, кто ты есть и что ты такое."
        "Ради чего?"
        play sound sfx_7dl["makarych"] fadein 0
        "Меня ждёт мой холодный город, стылые улицы и нахохленное безразличие бегущих куда-то прохожих-птиц."
        scene black with fade
        "Я не знаю другого. Ведь это мой дом."
        "А этому путешествию пора прекращаться."
        "Прекрасному путешествию."
        "Длиной в три дня лета."
        play sound sfx_bodyfall_1
        stop sound_loop fadeout 0
        play sound sfx_7dl["aunl"]
        show acm_logo_va_deep with moveinright:
            pos (1600, 1020)
        pause(4.4)
        with vpunch
    else:
        scene bg int_intro_liaz_7dl with fade
        stop ambience fadeout 2
        "Всё это — просто морозный сон закемарившего над колесом неудачника."
        "Почти песня."
        "Почти примостившийся на стенке гусар."
        play sound_loop sfx_bus_interior_moving fadein 0
        pause(2)
        scene black with fade
        $ renpy.music.set_volume(0.0, delay=2, channel='music')
        stop sound_loop fadeout 0
        "Впереди уже можно было разглядеть громаду моста."
        "Пришла в голову мысль, что было бы любопытно — а что случилось бы, если бы автобус упал вниз?"
        "Не страшно, не жутко, не интересно. Просто вяло любопытно."
        play sound sfx_7dl["aunl"]
        show acm_logo_va_deep with moveinright:
            pos (1600, 1020)
        pause(4.4)
        with vpunch
    stop music fadeout 3
    stop ambience fadeout 6
    scene black
    call screen alt_timer
    if alt_day_catapult:
        "Я открыл глаза."
        scene gameover with gopr
        play sound sfx_7dl["aunl"]
        show acm_logo_va_victim with moveinright:
            pos (1600, 1020)
        pause(3)
    return

label alt_day3_dv_stayhere1_old:
    play sound sfx_7dl["tousche"]
    play sound2 sfx_concert_applause
    show acm_logo_va_qte with moveinright:
        pos (1600, 1020)
    pause(9.4)
    with vpunch
    stop music
    scene bg ext_warehouse_blurry_7dl with flash
    $ renpy.music.set_volume(1.0, delay=2, channel='music')
    stop music fadeout 4
    play music music_7dl["red_hurricane_piano"] fadein 3
    "Ледяные объятья неохотно отпустили меня."
    "Прямо сквозь рубашку на плечо капнуло чем-то горячим."
    scene bg ext_warehouse_night_7dl with dissolve
    "Я не успел испугаться, и терпеливо ждал, что будет дальше."
    me "Кто-то решил об меня тормознуть?"
    "Лениво бросил я через плечо, а оттуда слышались лишь шмыганье носом и прерывистое дыхание."
    "Наконец я смог обернуться."
    show dv cry pioneer2 at center with dissolve
    "Алиса."
    "Плачущая Алиса."
    "Думаю, теперь она меня закопает как свидетеля собственного позора."
    show dv cry pioneer2 close with dissolve
    "Но вместо того, чтобы отпрянуть, она прижалась ещё сильнее."
    me "Что случилось?"
    "Нейтрально поинтересовался я."
    "И всё-таки голос немного запнулся, и эта запинка сказала ждущему уху куда больше, чем все на свете слова."
    dv "Я н-не…"
    "Её колотило истерикой или чем-то в этом духе. Я не слишком силён в гашении женских истерик, поэтому всё, что мне оставалось — это наметить некое подобие объятий. И ждать."
    "Только не очень крепких. А то опять подумает что."
    me "Что?"
    "Ровно спросил я."
    show dv guilty pioneer2 close with dissolve
    dv "Прости. Пожалуйста."
    "Тут она меня, конечно, удивила."
    me "За что?"
    me "Ты не сказала ничего, что шло бы вразрез с твоими убеждениями."
    me "А с собой надо в мире жить. Это я точно знаю."
    dv "Неправда… Я хочу, но я боюсь."
    me "Боишься того, что можешь почувствовать что-то?"
    me "Ну так это пройденный этап — ты уже показала, что я у тебя ничего кроме раздражения не вызываю."
    show dv sad pioneer2 close with dspr
    dv "Это неправда."
    "Она замотала головой."
    dv "Ты сам не знаешь, что ты несёшь."
    me "Несу я ровно то, что мне сегодня либо сказали прямо, либо дали понять."
    me "Я ведь не совсем тормоз."
    "Я усмехнулся."
    me "Хотя, ты, конечно, считаешь иначе."
    dv "Я боюсь, что однажды проснусь, а тебя нет."
    "Кажется ей удалось успокоиться, во всяком случае, настолько, чтобы её отпустила трясучка."
    show dv guilty pioneer2 close with dissolve
    "Я позволил себе улыбнуться."
    me "Если бояться, что твои игрушки кто-то отнимет, рано или поздно это произойдёт."
    me "И раз уж ты успокоилась — пошли умоемся, а дальше думать будем."
    dv "Л-ладно."
    stop ambience fadeout 2
    "Она шмыгнула носом и в потёмках, пока никто нас не видел, взяла под руку."
    scene cg d5_dv_island with dissolve
    play ambience ambience_forest_night fadein 3
    "Такого развития событий я не ожидал!"
    "После всех сегодняшних мальчишеств. После этой дурацкой беседы, из которой я понял только то, что ценнее в качестве союзника и товарища по играм, чем…"
    "И вдруг такие знаки внимания!"
    "Разумеется, я не заблуждался на собственный счёт — в конце концов, мне ведь однажды уже было семнадцать."
    "Привлекательность моя колеблется между нулем и отрицательными значениями."
    "Так что я чисто физически неспособен пробудить какие-то чувства."
    "Во всяком случае, в семнадцать лет точно не был способен."
    "Я попробовал освободить руку, но Алиса только усугубила всё, прижавшись ещё и всем телом."
    "Кровь бросилась в лицо, когда сквозь тонкую рубашку я ощутил жар её тела."
    "Не знаю, насколько ей было удобно идти, но у меня вдруг ноги будто деревянными стали."
    scene bg ext_washstand_night_7dl
    with dissolve
    play music music_list["sweet_darkness"] fadein 3
    play ambience ambience_forest_night fadein 3
    us "Тили-тили-тесто, жених и невеста!"
    "Донеслось от умывальников."
    show us laugh dress with dissolve
    "В темноте стояла невысокая фигурка, впрочем, по двум хвостам-ракетам я её узнал."
    me "Ульянка, ты чего в потёмках бродишь?"
    "Не отреагировал на дразнилку я."
    "Алиса тоже промолчала, кажется, доверив мне бразды правления."
    us "Хотела убедиться, что вы и в самом деле тили-тесто."
    show us normal dress with dissolve
    us "Зачем вам это надо, я не понимаю. Вы друг друга терпеть не можете, но, кажется, порознь вам ещё хуже."
    hide us with dissolve
    play sound sfx_open_water_sink
    play sound_loop sfx_water_sink_stream fadein 1
    "Она помахала рукой и скрылась в темноте, давая мне возможность умыть зарёванную Двачевскую."
    "Вода была ледяная, как и всегда, а я тоже бросил пару горстей воды в лицо, чтобы немного остудить пылающие щёки."
    "После того, что учудила Алиса…"
    "…Ульянка нас благословила, ну надо же!"
    stop sound_loop
    play sound sfx_close_water_sink
    show dv normal pioneer with dissolve
    dv "Я всё."
    "Её ещё немного колотило после сценки у склада, плюс холодная вода. А мне и согреть её было нечем…"
    "Во всяком случае, она расправила рубашку в бессильной попытке согреться."
    me "Хорошо."
    "Я нашёл её руку."
    me "Пошли тогда."
    "Я потянул её за собой, уводя к домикам."
    scene bg ext_houses_night_7dl with dissolve
    show dv surprise pioneer with dissolve
    dv "Что? Куда?"
    me "Для начала — ко мне в домик."
    show dv shy pioneer with dissolve
    dv "Что, прямо так сразу?"
    "Я пожал плечами."
    me "А чего тянуть? Лично мне там удобнее всего."
    dv "Ну… Не знаю… А если Ольга…"
    "Она смутилась."
    me "Да что она, не понимает, что ли. Дело-то молодое."
    "Я понизил голос."
    me "Да не бойся ты. Мы быстро. Раз — и готово!"
    show dv shocked pioneer with dissolve
    dv "…"
    me "Зная тебя, у нас всё быстро получится."
    "Кажется, я смутил её достаточно для того, чтобы чувствовать себя отомщённым."
    me "И вообще, на сон грядущий делать такие вещи доктора не рекомендуют. Говорят, вредно."
    me "Но ты же с ужина ничего не ела. Пошли!"
    show dv shy pioneer with dissolve
    "То, как она вспыхнула, было заметно даже при свете луны."
    th "Какая прелесть."
    dreamgirl "Поздравляю, мальчик, ты только что сдал вступительный экзамен в школу доктора Виолетты."
    dv "Ну… Я у Ульянки немного конфет свистнула."
    "Наконец справившись с собой, призналась она."
    scene bg ext_house_of_mt_night with dissolve
    play sound sfx_open_dooor_campus_1
    pause(2)
    stop ambience fadeout 1
    scene bg int_house_of_mt_noitem_night with fade
    pause(1)
    play ambience ambience_int_cabin_night fadein 1
    me "А я тебе ужин приготовил, скромно, но что есть."
    "Я распахнул перед ней дверь, пропуская внутрь."
    me "Булочки и кефир. Уж что нашлось."
    show dv smile pioneer far at center with dissolve
    dv "Теперь я тебе ещё больше должна."
    "Улыбнулась она без обычного своего высокомерия — почти как тогда, на пляже."
    "Но уговаривать себя не заставила и подмела всё подчистую, уплетая с таким аппетитом, что и мне кусочек захотелось."
    "Не досталось — зубы клацнули в опасной близости от вытянутой руки{nw}" with vpunch
    extend ", и я поспешил спрятать конечность."
    "Я улыбнулся и устроился на кровати, наблюдая за тем, как девочка ест."
    "Ещё пять минут напряжённой работы челюстями — и она сыто отвалилась от стола."
    me "Вот и чудненько. А теперь поговорим."
    play music music_list["tried_to_bring_it_back"] fadein 3
    show dv sad pioneer at center with dissolve
    dv "О чём?"
    "Она подняла глаза, чувствуя себя не слишком уютно тет-а-тет с полузнакомым парнем в домике."
    me "Да-да, именно о том, о чём ты подумала — о нас с тобой. {w}Как говорят американцы, о гёрлфрендах и бойфрендах."
    show dv guilty pioneer with dspr
    "Она покраснела, похоже, понимая меня несколько превратно, на что я поспешил заметить:"
    me "Не совсем об этом, хотя ход твоих мыслей мне нравится."
    dv "А что тогда…"
    "Она окончательно смутилась."
    me "Я буду чувствовать себя идиотом, но это, похоже, нужно для дела."
    "Я подполз к ней поближе и, взяв за руку, заглянул в глаза."
    me "Обычно это должно получаться спонтанно, но… Двачевская, что ты думаешь о том, чтобы стать моей девушкой?"
    show dv shy pioneer with dspr
    "Она смутилась ещё сильнее."
    dv "Я… Не умею."
    me "Не умеешь? {w}Я тоже не умею. Как-то не приходилось ещё ни разу быть чьей-то девушкой."
    dreamgirl "Кретин, что ты несёшь?!"
    th "Я решил, что немного комического контраста не повредит."
    dv "Да нет, я говорю, я раньше ни с кем не встречалась ещё."
    me "Я понял. {w=.2}Ну, мы все учились понемногу, чему-нибудь и как-нибудь."
    dv "Так а что делать-то надо?"
    me "Для начала перестать смущаться. А дальше…"
    stop music fadeout 4
    hide dv with dissolve2
    "Я помолчал, думая, куда на самом деле дальше-то…"
    me "Наслаждаться жизнью и компанией друг друга. По мере сил."
    "Наконец нашёлся я."
    play music music_list["farewell_to_the_past_edit"] fadein 5
    me "Ночь какая хорошая!"
    me "Пойдём прогуляемся?"
    "Она не ответила."
    me "Алис?"
    "Я обернулся в её сторону."
    "Мда… Похоже, я настолько скучный, что все существа женского пола в обозримом пространстве при моём приближении испытывают непреодолимое желание спать — начиная с Ольги, заканчивая Алисой."
    "Напереживавшись на год вперёд, молодой организм прибёг к самому простому способу восстановления после стресса — крепкому сну."
    "Улыбнувшись, я поднял её на руки и перенёс на кровать, а сам вышел на улицу, где и облюбовал себе один из шезлонгов, поворочался, устраиваясь поудобнее, и закрыл глаза."
    scene black with fade2
    "Через некоторое время ткань соседнего шезлонга скрипнула, принимая в свои объятья ещё кого-то, женский голос спросил, почему-то шёпотом:"
    voice "Спит?"
    me "Ага. Закономерное завершение истерики."
    voice "Вы с ней не…"
    me "Да нет, не сильно ругались."
    "Я сделал вид, что не понял намёка."
    me "Просто умыл, накормил, а уснула она сама."
    voice "Надо же…"
    "Голос казался удивлённым."
    voice "На моей памяти ты первый, кто сумел приручить Двачевскую."
    me "Достаточно было просто перестать врать себе."
    voice "Мне бы этому научиться…"
    scene bg ext_house_of_mt_night_without_light with dissolve
    stop ambience fadeout 1
    show un modern shade close at right with dissolve
    "Шезлонг опять заскрипел, на фоне звёздного неба силуэтом мелькнули четыре хвостика…"
    show un smile dress close at right with dspr
    me "Лена?!"
    "Ахнул я."
    un "А кого ты ожидал?"
    "Лукаво спросила она."
    un "Уж не Ольгу ли Дмитриевну?"
    me "Может, и её. {w}А что ты тут делаешь?"
    un "Медсестра попросила помочь в медпункте, раз уж я всё равно не танцую."
    un "Только что освободилась. Но если нужно валерьяночки…"
    me "Нет, спасибо."
    "Я поймал себя на мысли, что мне удивительно легко общаться с этой девочкой."
    "Будто не было нескольких дней взаимных смущений."
    "А ей, кажется, вдвое легче оттого, что лица прячет темнота."
    show un smile3 dress close with dissolve
    un "Береги её. Она сама не знает, какое она счастье. Не обижай."
    me "А как же…"
    if (herc or loki):
        un "Наша беседа перед турниром? Это наши внутренние отношения такие."
        show un laugh dress close with dissolve
        "Она рассмеялась."
        un "Выглядит шокирующе, понимаю."
    else:
        un "Что, смутила её манера поведения? Не обращай внимания."
        show un smile2 dress with dspr
        un "Это не она настоящая, я-то знаю."
    me "Да нет, я о другом. О её мании — она боится, что ты хочешь отобрать у неё все игрушки."
    "Я запнулся."
    me "В том числе…"
    show un smile dress close with dissolve
    un "Тебя?"
    "Она сидела близко-близко, я мог разглядеть каждую волосинку в её хвостиках, отблеск лунного света в глазах… Взгляд невольно притягивало к губам."
    un "И подраться с ней потом? Ну она скажет, конечно."
    un "Ты — это только её.{w} Это обоюдно, поэтому я даже лезть не стану."
    un "Спокойной ночи."
    hide un with dissolve
    "Как и Ульянка, она растаяла в темноте."
    show blinking with dspr
    "Я опять закрыл глаза и, кажется, уснул, потому что перехода между отключением сознания и началом неласковой такой тряски за плечо не ощутил."
    show mt smile dress with dissolve
    "Это была Ольга Дмитриевна, и она всеми силами пыталась выглядеть сердитой."
    "Что, впрочем, с довольным выражением лица сочеталось не очень."
    show mt normal dress at center with dissolve
    "А вот платье на ней было — закачаешься, и я поневоле проследил за всеми изгибами и выпуклостями прежде, чем понял, что она мне что-то говорит."
    mt "…филоним коллективные мероприятия, да?"
    mt "Сидим непонятно где, занимаемся непонятно чем. Кстати, почему именно здесь?"
    me "Там Алиса."
    "Односложно ответил я."
    mt "И она…"
    me "Спит."
    show mt normal dress at center  with dissolve
    "Не знаю, какая мысль пришла в голову вожатой, но она вдруг смутилась и расспросы на время прекратила, поднялась на крыльцо, приоткрыла дверь и задумчиво посмотрела на спящую в моей кровати девочку."
    mt "Я не буду спрашивать о том, как она здесь оказалась."
    mt "Но придётся тебе сегодня ночевать на незастеленной кровати."
    hide mt with dissolve
    pause(1)
    show mt normal dress at center with dissolve2
    "Она снова сходила в домик и принесла оттуда связку ключей, один в один с той, что посеяла Славя, когда кормила меня."
    mt "На следующей «улице» обрати внимание на «бочку» с эмблемой возле двери, она ещё заметно так от земли оторвана."
    "Она сделала строгий вид."
    mt "Надеюсь, в первый и последний раз."
    me "Я тоже."
    "Вздохнул я."
    me "Мне нравится спать в своей кровати."
    show mt smile dress with dissolve
    "Ольга вдруг хихикнула:"
    mt "Представляю завтра её лицо с утра. Обязательно разбужу её лично."
    me "Вы страшный человек, Ольга Дмитриевна. Так ведь и заикой можно стать!"
    mt "Ничего, меньше терять кавалеров будет."
    show mt smile dress with dissolve
    "Я покраснел."
    me "Да мы не…"
    mt "Да я знаю, чего вы не…"
    mt "Про то, как Сёма с Алиской обжимались у рукомойников, половина лагеря уже судачит. Сплетники."
    "Она сплюнула это слово как ругательное."
    mt "А я думаю, что главное, чтобы всё честно было."
    "Она многозначительно покосилась в мою сторону."
    mt "И по любви."
    "Я же только вздохнул, и, поднявшись, направился на своё временное место ночёвки."
    hide mt with dissolve
    scene anim_square_party with dissolve
    play ambience ambience_camp_center_night fadein 2
    sl "Семён! Семён! Погоди."
    "Я обернулся."
    show sl normal dress with dissolve
    "Славя, в руках какой-то куль."
    th "Очередная помощь страждущим?"
    me "Чего тебе?"
    sl "Да вот, услышала, что ты временно переезжаешь. Так я тебе подушку пока дам, ну и мыло с полотенцем."
    show sl smile dress with dissolve
    sl "Кстати сказать, сегодня баню растопили, так что если есть желание нормально помыться, могу занять очередь."
    "Прикинув, сколько времени я уже нормально не мылся, я согласно кивнул."
    me "Конечно, занимай. Спасибо."
    show sl smile dress with dspr
    sl "Хорошо."
    "Она улыбнулась и махнула рукой в сторону лесочка."
    sl "По тропинке иди мимо рощи, как раз и выйдешь. Не промахнёшься."
    "Она улыбнулась и, вручив мне подушку с банными принадлежностями, ушла."
    hide sl with dissolve
    stop music fadeout 3
    stop ambience fadeout 6
    with fade
    return