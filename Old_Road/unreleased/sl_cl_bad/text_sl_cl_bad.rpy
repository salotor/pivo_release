label alt_day6_sl_cl_home:
    scene bg int_semen_room_clean_7dl with dissolve
    $ set_mode_adv()
    play music music_7dl["misery"] fadein 3
    "Я проснулся от того, что включился монитор и меня ослепило ярким светом белоснежного фона окошка скайпа."
    "Очередной бот, неизвестно каким образом нашедший меня в сети."
    "Некая Киска1337, желающая продемонстрировать мне свои сомнительные прелести, ведь её так возбуждает бумажнопакетоголовый дурачок у меня на аватаре."
    "А всего-то и надо, что сходить по присланной ссылке."
    "Обычно я открываю по соседству окно с ботом и копирую им фразы, пока не надоест."
    "Но сейчас что-то не хотелось."
    "Сейчас между монитором и мной стояли лица, люди — живые, куда реальнее той реальности, чем та, в которой я живу."
    "Как я умудрился их выдумать, увидеть во сне?"
    "У меня всегда были проблемы с фантазией, я человек-шаблон, человек-алгоритм…"
    if herc:
        "И тогда Семён Сычёв"
    else:
        "И тогда Семён Персунов"
    extend ", пока ещё Сёмушка, пока ещё нужный и важный — безнадёжно застрявший в прошлом чужой земли, поднял голову и увидел, как тают дымными тенями те, кого он любил, ненавидел."
    "Выдумал."
    "Уходит Миронова Ольга, вожатая, в панаме, в белоснежной рубашке с ромбиком отличницы, фигуристая, принципиальная, добрая."
    "Тает Коломийцев Борис, держащий за руку Коллайдер Виолу — две струйки дыма, в пурпуре и серебре, переплелись сизыми объятьями со стороны фильтра, со стороны угля."
    "Уходят Мику, Алиса, Лена, прекрасные, как летнее солнце в небе фантастического лагеря, что никогда не существовал."
    "Остался только Я."
    "Я, сидящий в кресле перед экраном, съёжившийся, сгорбившийся, щурящийся на белый фон."
    "Я, опирающийся локтями о колени в подземке, везущей в никуда."
    if loki:
        "Я, смотрящий как снег окрашивается алым, ждущий, когда костлявая сомкнёт ледяные объятия."
    elif herc:
        "Я, выкашливающий остатки нежелания заканчиваться так бесславно, на грязном полу магазинчика с алкоголем и спиртным."
    else:
        "Я, тонущий как слепой котёнок, и не пытающийся трепыхаться."
        "Слишком уж давно мечтал о смерти и слишком большим грехом почитал суицид — мог ли я упустить такую шикарную возможность?"
    dreamgirl "Ну что, как тебе экскурс в несуществующее?"
    "Ехидством в голоcе альтер-эго можно было разъесть бетонные сваи близнецов."
    dreamgirl "Была хоть польза от твоего огуливания красотки с косой или ты так, за титьки подержаться?"
    dreamgirl "Не подумай, что я докапываюсь на ровном месте, просто мне интересно, какой смысл вообще был ступать на самую трудную дорожку, если в результате ты оказался здесь же, откуда начинал."
    dreamgirl "Хотя у тебя тут почище, как я погляжу. Тоже ничего себе прогресс."
    dreamgirl "Сам убирался?"
    me "Да. {w}Я понял всю ценность людей. Хотя это неправильно как-то звучит. Как будто я их на рынке покупаю."
    me "Вот так — я понял всю их бесценность. Каждого человека по отдельности, всех вместе, коллектива, социума."
    me "Я не скажу, что полюбил людей, но ценить их — научился."
    "Я говорил, обращаясь больше к себе, чем к Нему."
    "И не знал, зачем я говорю это."
    dreamgirl "Иными словами, ты понял то, что должен был понять ещё в первом классе."
    dreamgirl "Не самый впечатляющий показатель."
    me "Но я хочу помогать людям."
    dreamgirl "Да? Как?"
    if loki:
        dreamgirl "Много ты им напомогаешь на свои учительские гроши?"
    elif herc:
        dreamgirl "Раздашь им получку за последнее дежурство?"
    else:
        dreamgirl "Пойдёшь раздашь свой заработок со фриланса?"
    me "Я пока не знаю."
    dreamgirl "А кто знает? Дурацкое желание — помогать. Ты никому помочь не можешь, ты себе помочь не в состоянии."
    dreamgirl "Стандартный, быстро увлекающийся и столь же быстро остывающий дурачок, вдохновившийся примером."
    dreamgirl "Дурак, а дурак, ты меня слышишь?"
    me "Слышу."
    dreamgirl "Славя твоя этому жизнь отдаст, учиться будет тому, как лучше всего помогать людям, и у неё никогда не будет другой жизни, кроме жизней других людей."
    dreamgirl "Тебе этот прайс не по карману."
    me "Я был рядом со Славей, когда она перестала справляться, ясно тебе? Она надеялась на меня."
    dreamgirl "Велика важность — утешить плачущую девушку."
    dreamgirl "Что хорошего ты сделал в том лагере, в несбыточных, тепличных условиях — аллювии для супергероев всех мастей — о чём сможешь вспомнить и гордиться лет через пять?"
    dreamgirl "Исключая то, что ты закономерно влюбился и теперь ради неё и правда готов горы своротить."
    me "А этого мало?"
    dreamgirl "Да только ведь любовь живёт три года, да и Славя твоя… Не факт что существует."
    dreamgirl "То есть, ты выбрался из болота ради того, чтобы встать в тени Слави. В тени несуществующей девушки, которой тоже иногда хочется сильного мужского под жилеткой и «кто хозяин в доме»."
    if loki:
        dreamgirl "Посвятил свою жизнь мелкой мести человеку, который оказался большим мужиком, чем ты."
        dreamgirl "Даже имя себе взял — Лооооки. {w}Дешёвка из комиссионного магазина."
    elif herc:
        dreamgirl "Ты всю жизнь пытался сбежать от горькой памяти собственных поражений."
        dreamgirl "По сути, нескольких неурядиц, что должны были лишь укрепить, сделать сильнее, а вместо этого заставили сесть на пенёк и заплакать."
        dreamgirl "Ветеран горячей точки, почти журналист, почти музыкант. Фуфло."
    else:
        dreamgirl "Всю жизнь бежал от переломанной  в аварии жизни — а пришёл к новой, зато выдуманной."
        dreamgirl "Даже своё призвание — и то нашёл дурацкое. Помощник по лагерю? Ты серьёзно? Метёлок начальник и ключей командир?"
    me "Я стал лучше."
    "Я всё ещё обращался к тени в углу."
    me "Я больше не вернусь к старой жизни, слышишь?! Я изменился."
    dreamgirl "Ты выжил в пионерском лагере. Практически, прошёл невероятно сложную игру, где самым трудным было не умереть со скуки."
    dreamgirl "И теперь считаешь, что победителей не судят. Ведь ты же победитель, верно?"
    dreamgirl "Победятел."
    me "Я восстановлюсь в ВУЗе, я попробую сначала, чтобы тебе не пришлось стыдиться меня, если однажды встретишь на улице!"
    dreamgirl "Ты носишь ауру неудачника. Знаешь, как дети, которых постоянно все бьют. Откуда дети знают, кого можно бить, а кого нельзя? Они чувствуют её."
    scene bg semen_room_window with dissolve
    dreamgirl "Ты выбрал сам эту петлю, ты знаешь это, да?"
    dreamgirl "Сдаться и сломаться. А запала лагеря не хватит надолго."
    dreamgirl "Полгода, край год. А однажды твоя валькирия встретит тебя на улице. И отвернётся — потому что ты нелюдим, небрит, угрюм и на лбу набрякла ненависть."
    dreamgirl "Ты уже сейчас потенциально он. {w}Потому что у тебя никого нет. И единственный твой собеседник — это твоя шизофрения."
    "За окнами проорали сирены полицейского экипажа, наверху грохануло чем-то тяжёлым, из соседнего двора донеслись переливы дешёвой, визгливой автосигнализации."
    "Жизнь продолжалась, протекала сквозь меня, переливаясь на ледяном стекле утеплённых окон, что так и не заменил на стеклопакеты, всю жизнь клеил бумагой."
    "И не хотелось уже возвращаться куда-то, переделывать что-то, менять, мешать, выбирать, творить — к чёрту. Я закончился."
    "И только его правда была кругом, язвы этакой: я классический неудачник, в жизни которого ничего никогда не происходит и не произойдёт."
    stop music fadeout 3
    return

label alt_day6_sl_cl_intellect:
    scene black
    play music music_7dl["gonna_be_ok"] fadein 3
    play sound sfx_intro_bus_engine_start
    pause(3)
    play sound_loop sfx_intro_bus_engine_loop fadein 3
    $ set_mode_adv()
    scene anim intro_9
    show prologue_dream
    with fade
    "Забиться в тёмный угол и досидеть там до звонка, ожидая, когда же наконец отпустят в небытие."
    "Память понемногу осыпалась невесомым пеплом, гонимым ледяным декабрьским ветром."
    "Плеер сдох. Телефон на последнем издыхании."
    "Зато дата правильная — 28 декабря 2018-го года."
    "Первая ступенька в небо."
    "Что там дальше? Краснопузый ЛиАЗ с совковой попсой в салоне?"
    pause(2)
    scene anim intro_10
    show prologue_dream
    with fade
    play sound sfx_intro_bus_door_open
    pause(3)
    scene anim intro_11
    with fade
    pause(1)
    stop sound_loop fadeout 4
    scene anim intro_13
    show prologue_dream
    with fade2
    pause(3)
    scene bg intro_xx
    show prologue_dream
    with fade
    "Забавно почувствовать себя пророком, зная наперёд всё, что произойдёт."
    "Как будто кто-то бросил отсверком тень будущего мне на сознание."
    "И я считаю секунды до того, как произойдёт что-то, что уже успел подсознательно отметить — повернувшуюся женщину, прыгающего ребёнка, сигнализацию машины, сработавшую от снежка в лобовое."
    "Даже полусорванную рекламу на развязке отметил."
    "Так, но это, наверное, всё будет попозже?"
    "Для начала неплохо было бы просто дойти до остановки."
    play sound sfx_intro_bus_stop_steps
    pause(3)
    scene anim intro_4
    show prologue_dream
    with fade
    "Я сам не знал, что делал на улице, но сидеть дома почему-то казалось кощунственным."
    "Будто нечто властное поселилось внутри меня и тянуло, тянуло вперёд."
    "Когда? А вот это отдельный вопрос."
    "Я думаю, тогда, когда раздвоение личности исчезло, я перестал слышать его голос — то есть, вообще слышать, и внутри, и снаружи. Всё, моё сердитое, ядовитое альтер эго почило в бозе, прозит!"
    "Поэтому я иду, безуспешно пытаясь спрятаться от мороза под пальто, не предназначенном для такой температуры."
    "Верно, верно, дрищ должен страдать."
    scene anim intro_3
    show prologue_dream
    with fade
    "Вот и страдал сначала до подъезда."
    "А потом мимо привычных, въевшихся в память многоэтажек — по заснеженным улицам, где холодно от одного только взгляда на замерзающие в воздухе лучи фонарей."
    stop music fadeout 3
    scene bg bus_stop with diam
    pause(2)
    scene bg intro_xx with fade
    play sound_loop sfx_bus_interior_moving fadein 4
    voice "Ой, простите, пожалуйста, я вас не сильно зашибла?"
    me "Не сильнее, чем Маша Медведя."
    "Привычно ответил я, не глядя на виновницу происшествия."
    me "Главное, чтобы привычкой не стало."
    voice "А почему вы так спокойно к этому относитесь?"
    "Я пожал плечами и посмотрел на неё."
    "Взгляд почему-то никак не хотел фокусироваться."
    stop sound_loop fadeout 3
    show blink
    play music music_7dl["tilltheend"] fadein 1
    pause(1)
    scene bg ext_square_night
    show un sad pioneer
    show prologue_dream
    with dissolve
    sl "Лена?"
    un "Привет, Славушка…"
    "Она казалась ненормально бодрой и весёлой."
    un "Я вот тут шла и шла."
    "Она улыбнулась."
    un "А меня бросили."
    "С трудом сдерживаемые рыдания прорвались наружу, она опала там же, где стояла — я еле-еле успел подхватить её у земли."
    sl "Как бросили?! Ты же говорила, что вы после лагеря собирались…"
    un "Похоже… Похоже, что это я собиралась. А он как будто проснулся."
    "Она всхлипнула."
    un "Я пошла на карьер, чтобы оставить там наше сердечко, а когда вернулась…"
    show un cry pioneer with dspr
    un "Я так не могу больше. Если он несвободный, зачем вообще что-то было начинать? Так же нельзя!"
    scene bg ext_aidpost_night
    show un serious pioneer
    show prologue_dream
    with dissolve
    un "Ещё и ты. Объясни, зачем ты вообще вмешиваешься?"
    sl "Ты моя подруга. И мне очень важно, чтобы с тобой всё было хорошо."
    un "Ты не видишь, что ничего хорошо не получается?"
    un "Можно я уже закончу всё? Устала я от боли."
    "Она выглядела задёрганной, замученной, и с губ естественно сорвались слова — неправильные, некрасивые, но они были единственным, что способно сейчас помочь."
    sl "Хорошо, ты можешь закончить. {w}Но просто по дружбе сделай мне одолжение, пожалуйста."
    un "Какое?"
    sl "Дай миру шанс, последний, единственный."
    sl "Мы приедем сюда в следующем году, и ты попробуешь в последний раз. Хорошо?"
    show un normal pioneer with dspr
    un "И если не получится, ты больше не станешь мне мешать?"
    sl "Обещаю. Я даже не останусь на пересменок."
    un "Поклянись."
    "И я клянусь."
    "Я знаю, что эту клятву нарушить не сумею — Лена знает, чем заставить поклясться, не зря же мы с детства дружим."
    scene black with diam
    pause(1)
    scene bg ext_warehouse_night_7dl
    show prologue_dream
    with dissolve
    "В мешке слышно слабое шевеление, а я не могу, не смею рассказать всего, чтобы не повлиять как-то на решение."
    "Моё право — донести знание."
    "Настаивать или заставлять я не могу. Не смею. Тем более, от боли чужого несостоявшегося счастья я потом просто стоять на ногах не смогу."
    "А он смотрит спокойно и отрешённо как-то — он вообще ничего не чувствует, похоже."
    me "Да. Я не против вернуться домой."
    with fade
    "Он забирает мою руку в свою, забирает руку Алисы, зачем-то увязавшейся с нами, и…"
    "Всё зря. Мы изобразили дурацкую сцену, встав в круг, но ничего не произошло — как будто он ещё не закончил здесь всего, что должен был."
    "Он не закончил."
    "А вот Пират…"
    with fade
    "Я выскакиваю из автобуса, моя остановка — мне очень горько от того, что мне пришлось сделать."
    "Очень стыдно, что пришлось выламывать руку, но…"
    "Моё внимание привлекает мигающая жёлто-оранжевым неоном вывеска, изображающая стилизованный отпечаток собачьей лапы."
    "Моя остановка."
    scene black with diam
    pause(2)
    scene cg d6_sl_clean_7dl
    show prologue_dream
    with dissolve
    "Шурх. Шурх."
    "Иногда я просто сдаюсь. Я сажусь на попу и беззвучно вою в тёмный потолок склада."
    "Мне страшно от того, что все усилия пусты."
    "Метла разгоняет клубы пыли, листья, бумажки, обёртки от конфет."
    "Мне больно от того, что я слишком поздно — узнаю? Вспоминаю? Понимаю? Я не знаю, как это назвать, но знание просто приходит ко мне."
    "Как это было с Леной, с Алисой, с Мику, с Олей."
    "Пусть уж лучше сидит здесь, на скамейке, под приглядом."
    "Как это происходит сейчас, пока я старательно отворачиваюсь от него, пряча непрошенные слёзы — мне не хватило нахальства прямо попросить его вернуться домой, значит, я теперь косвенно отвечаю и за его счастье."
    "Надо бы завтра заставить его хотя бы попытаться помириться с Алисой."
    scene bg ext_city_night_7dl
    show prologue_dream
    with dissolve
    "Он стоит на экскалаторе ступенькой ниже, смотрит снизу вверх."
    "Наверное, хотел устроить романтическое «глаза в глаза», балбес. {w}Про трёхдюймовую шпильку, надо понимать, он забыл."
    "Телефон жужжит, принимая сообщение, но я не успеваю прочесть его — у меня тут же отнимают аппарат, читают, что-то пишут в ответ и, полностью отключив, возвращают."
    "Чуть позже я читаю «вообще, у неё есть молодой человек. Надеюсь, понятно?!»"
    "Ох уж, эта ревность."
    "Ох уж, эти Олины шуточки."
    scene anim intro_1
    show prologue_dream
    with fade
    $ renpy.pause(3, hard=True)
    scene anim intro_2
    show prologue_dream
    with fade
    play sound sfx_intro_bus_stop_steps
    $ renpy.pause(3, hard=True)
    scene anim intro_3
    show prologue_dream
    with fade
    "Всё это в прошлом — сейчас я лежу на горячем сталепластике капота, бессильно пытаясь сделать вдох."
    "Понимаю, что между бампером и столбом места действительно маловато для человеческого тела, значит, всё куда грустнее, чем казалось."
    sl "Почему так неправильно всё, Сёмушка?"
    "Самое глупое, что можно было сказать, угасая."
    scene black with fade
    pause(2)
    scene black with fade
    stop music fadeout 4
    $ alt_pause(2)
    play sound_loop sfx_bus_interior_moving fadein 1
    $ alt_pause(1)
    scene anim intro_13
    with fade2
    $ alt_pause(3)
    scene bg intro_xx
    with fade
    "Я моргнул и наваждение пропало. Что это было?"
    "С трудом вспомнив, о чём меня спрашивали, я наконец ответил."
    me "Наверное, потому, что я уже спас вас, уклоняться поздно."
    "Девушка улыбнулась:"
    voice "Ну и забавный же вы. Как вас зовут?"
    me "Моё имя Семён."
    voice "Семён?"
    "Она попробовала звучание имени."
    voice "Семён как Сёмушка?"
    menu:
        "Да, как Сёмушка":
            scene anim intro_8
            "Этой улыбке невозможно было сопротивляться."
            me "Сёмушка."
            "Кивнул я."
            $ alt_day6_sl_cl_int = 2
        "Нет, как Семён":
            "Сёмушкой меня могла называть всего одна девушка."
            "Только ей было можно."
            "Плевать на то, что её не существует."
            scene anim intro_8
            "Девушка немного погрустнела:"
            voice "Простите, пожалуйста. Я не знала, что у вас плохое настроение."
            me "Да ничего."
            "Я пожал плечами и вернулся к себе на место над колесом, заткнув уши наушниками."
            "Немного посомневавшись, она не стала садиться рядом, а ушла на заднюю площадку — я проводил её взглядом."
            "И, схватившись за поручень, встала там."
            "Наверное, обиделась."
            "Плевать. У меня старая школа, старая закалка — девушки не нужны, никто не нужен, ото всех только проблемы."
            "Когда-то я считал иначе."
            "И очень больно обжёгся на этом."
            "В памяти застарелым ожогом вспыхнул промозглый июльский полдень в далёкой-далёкой стране."
            "И я затряс головой, старательно прогоняя воспоминания."
            th "Где ж теперь моя шиза, когда она так нужна?"
            "Издевательства издевательствами, но чёрную память от памяти светлой моё психическое расстройство отделяло замечательно."
            "Правда закончилось всё ядовитым монологом у меня в квартире — видимо, я и в самом деле безнадёжен."
            "В голову вдруг пришло, что я понятия не имею, куда еду."
            "И зачем?"
            "Так что на ближайшей остановке я вскочил и вышел из салона."
    stop music fadeout 3
    stop ambience fadeout 6
    with fade
    return

label alt_day7_sl_cl_fear:
    $ set_mode_nvl()
    scene bg bus_stop with dissolve
    play music music_7dl["prologue_2"] fadein 3
    play ambience ambience_cold_wind_loop fadein 3
    "Когда мы выходили из автобуса, я подал девушке руку, которую она с благодарностью приняла."
    "Правда, пронёсшийся мимо мотоциклист заставил нас обоих вздрогнуть от неожиданности."
    "Меня — ещё и потому, что волосы у девушки опять странно зашевелились — так, будто она стригла ушами по-кошачьи."
    me "Тут шумно."
    "Крикнул я."
    "Старательно не обращая внимания на странности."
    th "Если не замечать это, оно пройдёт."
    "Твердил я волшебную мантру стабилизации реальности."
    voice "Да, суетно как-то. {w}Может быть, отойдём куда-нибудь, где потише, да поболтаем?"
    th "Ничего себе."
    "Вяло удивился я."
    th "Да со мной же знакомятся!"
    th "Вернее сказать, уже познакомились и сейчас семимильными шагами развивают знакомство."
    th "Интересно, что ей может быть нужно?"
    "В своих обычных традициях я предполагал худшее — но что сейчас может быть худшим?"
    th "Заведёт в тёмный угол и там надругается?"
    th "Напоит клофелином и сбежит, прихватив с собой все мои триста рублей?"
    th "Ага, очень смешно."
    nvl clear
    scene bg int_caffee_day_7dl with dissolve
    voice "Сёмушка, расслабьтесь."
    "Заявило это невозможное создание, затащив меня в какое-то кафе, где у стен в нишах, отделённых друг от друга звуковыми перегородками, загорали напротив огромных окон во всю стену уютные даже с виду диванчики."
    "Стоит ли говорить, что села она не напротив, как это предполагалось бы, а плюхнулась рядом?"
    voice "Вы ведёте себя так, будто меня боитесь."
    me "Что, я?"
    "Мой смех прозвучал неестественно."
    voice "Ну не я же! {w}Меня вообще всегда бесили условности, обнюхивание человека, притирка. Зачем?"
    voice "Вы спасли меня, подставив собственное тело, и даже не обругали — всё, вы мне нравитесь, живите теперь с этим."
    me "Не слишком рисковая позиция?"
    nvl clear
    "Спросил я."
    voice "Нисколечко. {w}Экономит уйму времени и сил. {w}А у вас есть самый большой страх, Сёмушка?"
    me "Самый большой страх?"
    "Я задумался."
    "Мысль о том, что по большому счёту всё это не её дело, больше в голову не приходила."
    "Длинные каштановые волосы, глаза карие до жёлтого и нахальная улыбка."
    "Какие уж тут тяжёлые мысли?"
    nvl clear
    voice "Да. {w}Есть чего-то, чего вы очень боитесь?"
    me "Да глупостей всяких."
    me "Вроде дырки в штанах."
    "Честно признался я."
    me "В детстве у меня несколько раз происходили казусы с брюками, и поэтому я очень боюсь оказаться на людях с таким вот недочётом в одежде."
    voice "Какие казусы?"
    "Она заинтересованно подалась ко мне."
    me "Например…"
    "Я погрузился в воспоминания."
    stop music fadeout 3
    stop ambience fadeout 6
    with fade
    return

label alt_day7_sl_cl_1996:
    scene anim_digi with dissolve
    play music music_list["a_promise_from_distant_days_v2"] fadein 3
    pause(3)
    play sound_loop sfx_intro_bus_engine_loop fadein 3
    "И не понял, в какой момент мои воспоминания оказались явью."
    "Потому что сцена, с которой всё начиналось — вот она."
    scene bg int_bus_people_day with dissolve
    "За спиной на последних миллиамперах подвывал Петлюра, всё медленнее и медленнее — на кинематику не хватало заряда, пассик тянуло, кассета играла ощутимо медленнее."
    "Рядом Вавик ковырял синдром эпохи — гнутый кирпич «тетриса», у которого болты замазаны красной краской."
    "Чтобы пионерия не лазила — будет заметно."
    "А так залез — на гарантию пролетел. {w}У наших ремонтных гениев с этим было сурово."
    "С передних сидений доносился зычный бас Тамары Петровны, которая за буйный нрав и тяжёлую руку звалась в наших узких рядах Мотарой Треповной."
    "Ей отвечал Федюня, единственный, кому всегда не везло во время раздачи свободных мест в «Икарусе»."
    th "Мать честная, где это я?!"
    nvl clear
    "Вспыхнул в голове вполне себе актуальный вопрос."
    th "Я что, я…"
    if herc:
        voice "Филин, ты на батарейки попал, ты в курсе?"
    else:
        voice "Пересунь, ещё раз попытаешься поставить на зарядку неразряженные батарейки, посажу негром на динамо-машину, капишь?"
    me "Да я случайно!"
    "Услышал я собственный голос, высокий и писклявый."
    "Детский."
    me "Я же не знал, что их надо просто постучать немного!"
    voice "Больше «тетрис» не получишь."
    me "Да и не надо."
    "Перед глазами вспыхнули воспоминания о грядущем — досконально, вплоть до режуще-бордового цвета занавесок и нестерпимой бензиновой вони, с которой не справлялась открытая форточка."
    "Здесь я должен сделать ему «мультик», он ударится губой о ручку переднего сиденья — до слёз, после чего у нас случится драка, я где-то зацеплю шуруп или гвоздь."
    "Замру, когда раздастся треск — а поздно уже."
    "Одно из самых забавных воспоминаний детства — но забавным оно стало только сейчас, почти двадцать лет спустя."
    "Так что вместо следования памяти я удержал руку."
    nvl clear
    "Дальше меня ожидало позорное шествие вверх по дороге с драными штанами к самому начальнику лагеря, и первая половина смены была безнадёжно испорчена."
    "Я помню это, потому что в тот год я познакомился… {w}А впрочем, не имеет значения — важнее то, что должно было произойти сейчас."
    "Дети честны и жестоки. В приличном обществе все сделали бы вид, что ничего не произошло. {w}В детском же лагере…"
    "Сколько смеха было, Господи… Сколько дразнилок про сверкающий образ… {w}Я не чурался и кулаки пустить в ход, если того требовало окружение, но не будешь же драться со всем лагерем!"
    th "Но я же могу…"
    "Я поднял к глазам руку — крохотную, с негрубой ещё кожей. Пошевелил пальцами, понимая, что полностью контролирую себя."
    "А то я начитался в своё время рассказов про путешественников во времени, что вынуждены были бессильно наблюдать за одним и тем же развитием событий."
    th "Я могу делать что и как угодно? {w}Никаких сценарных ограничений? Никаких рельсов, рамок и прочего?"
    "Но это значит, что я не обязан делать то же самое и поступать так же, как тогда!"
    me "Вав, слушай…"
    "Он вопросительно посмотрел на меня."
    nvl clear
    $ set_mode_adv()
    menu:
        "Какой год сейчас?":
            voice "Ты наркоман, что ли? Или опять своего «назад в будущее» насмотрелся?"
            "Вопрос действительно был глупым, но самое главное сейчас — сохранять хорошую мину при плохой игре."
            me "Так ты, значит, года не знаешь?"
            voice "Знаю."
            "Осторожно ответил он. И правильно — репутация у меня была та ещё."
            me "Так скажи мне."
            voice "Пошёл ты."
            "Всё ещё ожидая от меня какого-нибудь глупого розыгрыша."
            me "На вопрос отвечай. {w}Или ты не знаешь? Я пойду Мотаре пожалуюсь тогда, пусть тебя в класс «б» переводят."
            me "Там все такие же тугие."
            voice "Отвали."
            "Зарычал он."
            "Неплохой парень, в принципе. Но слишком уж зависим от общественного мнения."
            voice "Девяносто восьмой год сейчас, ясно? Девяносто восьмой. {w}А теперь отстань от меня."
            "Он замер, ожидая от меня какой-то пакости, но я, тихо поблагодарив его, отвернулся к окну."
            stop music fadeout 3
            $ set_mode_adv()
            $ alt_day6_sl_cl_int = 3
            "Я честно решил, что ослышался."
            "Потом — что надо мной подшутили."
            scene stars
            $ renpy.show("bg ext_road_night", what = D3_intro("bg ext_road_night"))
            show prologue_dream
            with dissolve
            play music music_list["trapped_in_dreams"] fadein 3
            "В голове плотным клубком вертелись события последних суток — марш-бросок по расколоченной вторичке на скоростях, которые даже экстремал назвал бы самоубийственными."
            "Пират, о судьбе которого я так и не узнал ничего."
            "Пробуждение в автобусе — то самое, которого я всегда подспудно опасался, а потому не удивился ничуть, открыв глаза в холодном салоне."
            "Хотя сравнивая, конечно, с тем, чего я на самом деле ожидал — подарок судьбы, не меньше."
            "Девушка эта, её падение — странная сценка, сворованная из чужой памяти — наше знакомство…"
            "Хотя какое это знакомство, если мы номерами даже не обменялись?"
            scene bg int_bus_people_day with dissolve
            "Теперь вот «Икарус», везущий меня в лето 98-го."
            "С одной стороны, слишком бредовато для того, чтобы счесть это реальностью, с другой — слишком логично, чтобы счесть совсем уж откровенным бредом."
            th "Так где же я, чёрт возьми?"
            "А?"
            "Где! Я!" with vpunch
            voice "В дверях не толпиться, выходим по очереди, кто побежит, будет у меня помощником до конца смены."
            scene anim_digi
            $ renpy.show("bg int_bus", what = D3_intro("bg int_bus"))
            with dissolve
            "Пригрозила Треповна."
            "Работает — безотказно!"
            "Самый лучший якорь реальности — всю метафизическую чушь мигом из головы выметает."
            "Впереди одно из самых счастливых воспоминаний, не омрачённое практически ничем."
            "А я забился в дальний угол, выхожу последним, уставился, вперился взглядом в происходящее за окном — и посмотреть есть на что!"
            "За окном уплывает назад частный сектор, за окном уже видно стоящих у ворот людей, которые будут с нами рядом эти месяцы."
            "Я не знаю, мне с моей позиции взрослого человека это просто непонятно — но они радуются нам!"
            "Впереди кто-то заулюлюкал, замахал, игнорируя призывы сопровождающей, выпуская накопившийся за время дороги восторг."
            "Ведь мы приехали в лето."
            "Мне пришло в голову, что если я здесь и сейчас задержусь — это будет не десять лет выигрыша, а куда больше."
            "Притом, что всё произойдёт в моей объективной реальности."
            "Как в том старом анекдоте — а что, если ботаны, заучки, малолетние гении и таланты — это старики и старухи, которым кем-то был дарован второй шанс?"
            "Воплощённая взрослая мечта — и не странным образом извращённая, как это произошло в «Совёнке», а c соблюдением духа и буквы этой мечты."
            "Главное, чтобы это не оказалось иллюзией. Хотя, даже если это и она — пора получать удовольствие от детства!"
            "Если это игра — я готов в неё сыграть!"
            "Автобус остановился, скрипнув тормозами, нас качнуло вперёд, спереди зашипела пневматика, и мы, наплевав на все призывы к порядку, кинулись в тёплое белое сияние."
            "И я — вместе со всеми."
            stop sound_loop
            scene black with fade2
        "А куда девушка делась?":
            $ prolog_time()
            voice "Девушка?"
            "Голос Вавика оказался подозрительно похож на девичий — хотя в этом возрасте они у всех высокие."
            "И я открыл глаза."
            stop sound_loop
            scene bg int_caffee_day_7dl
            show unblink
            "Снова оказавшись в кафешке."
            me "А, вы здесь…"
            "Кажется, она от меня не отстанет."
            voice "Да, я спрашивала, какой у вас самый большой страх."
            me "Боюсь, это не та тема, которую я готов обсуждать с первой же встречной."
            stop music fadeout 3
            $ set_mode_adv()
    stop music fadeout 3
    stop ambience fadeout 6
    with fade
    return

label alt_day7_sl_cl_int_bad:
    scene black with dissolve
    play music music_list["you_lost_me"] fadein 3
    "Девушка виновато улыбнулась мне, кивнула, отчего русая шапка волос пришла в движение."
    "Показалось, или у неё из-под волос проглянули два кошачьих ушка?"
    scene anim intro_13
    th "Очередная нека-повёрнутая. Хорошо, что я не стал развивать знакомство."
    "Не тратя времени даже на соблюдение видимости вежливости, я ушёл оттуда."
    "У меня и так дел хватает."
    "Надо перейти на другую сторону проспекта, взять в ларьке пачку сигарет, вдохнуть сладковатую горечь, снова вспоминая о том, что курение на морозе — первый способ застудить себе лёгкие."
    "Меня ждёт путь домой."
    "Туда, где нет никаких сложностей и люди не делают больно друг другу."
    "В безликое сетевое существование."
    play sound sfx_7dl["aunl"]
    show acm_logo_sl_cl_int_bad with moveinright:
        pos (1600, 1020)
    pause(7.4)
    with vpunch
    return
